# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=camlp5
pkgver=6.02.0
pkgrel=2
pkgdesc='A preprocessor-pretty-printer of OCaml.'
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('sh')
makedepends=('ocaml')
conflicts=('camlp5-transitional')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/${pkgname}-${pkgver}.tgz"
        "META")
md5sums=('78ee9ea1b4f8e05af3f85fcbe14a7379'
         'c73a171979417787ef6c24bd65a5dd75')

options=('!makeflags')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict 

  make world.opt 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install 

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -m644 ${srcdir}/META ${pkgdir}/usr/lib/ocaml/camlp5/META
}
