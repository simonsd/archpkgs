# Maintainer: Thomas S Hatch <thatch45 at gmail dot com>
pkgname=apache-qpid
pkgver=0.8
pkgrel=1
pkgdesc="An amqp system written in C++"
arch=('i686' 'x86_64')
url="http://qpid.apache.org/index.html"
license=('APACHE')
depends=('openais')
makedepends=('e2fsprogs' 'boost' 'python2')
optdepends=()
source=("http://www.reverse.net/pub/apache/qpid/0.8/qpid-cpp-$pkgver.tar.gz")
md5sums=('08007df4b7e0fe06b108d9024413f9d2')

build() {
  cd "$srcdir/qpidc-$pkgver"
  
  ./configure \
    PYTHON_LIB=$(python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()") \
    PYTHON=python2 \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  for PY in $(grep -rn '/usr/bin/env python' * | cut -d: -f1)
  do
    sed -i 's,^#!/usr/bin/env\ python,#!/usr/bin/env\ python2,' $PY
  done
  make
}

package() {
  cd "$srcdir/qpidc-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
