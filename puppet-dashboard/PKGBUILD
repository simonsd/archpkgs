# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=puppet-dashboard
pkgver=1.0.4
pkgrel=1
pkgdesc='Web tracking frontend to puppet'
arch=("any")
url="http://www.puppetlabs.com/puppet/related-projects/dashboard/"
license=("GPL")
depends=('ruby1.8' 'rubygems1.8' 'ruby1.8-rake' 'mysql-ruby1.8')
install='dashboard.install'
source=("http://puppetlabs.com/downloads/dashboard/$pkgname-$pkgver.tgz"
        "puppet-dashboard")
md5sums=('27470fc9e66dc37f15959950c8bb9926'
         '689604f3893ac1403f8f77b4070e78da')

package() {
  cd $srcdir/puppetlabs-$pkgname-071acf4

  # Directories
  install -d $pkgdir/usr/share/$pkgname/{log,public,tmp}

  # Install
  cp -p -r app bin config db ext lib public Rakefile vendor script spec $pkgdir/usr/share/$pkgname/
  install -Dp -m0644 config/database.yml.example $pkgdir/usr/share/$pkgname/config/database.yml
  install -Dp -m0644 RELEASE_NOTES.md $pkgdir/usr/share/$pkgname/RELEASE_NOTES.md
  install -Dp -m0644 VERSION $pkgdir/usr/share/$pkgname/VERSION

  # rc script
  install -Dm 755 $srcdir/puppet-dashboard $pkgdir/etc/rc.d/puppet-dashboard
}
